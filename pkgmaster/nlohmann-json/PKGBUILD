pkgname=nlohmann-json
pkgver=3.1.0
pkgrel=1
pkgdesc='JSON for Modern C++'
url='https://github.com/nlohmann/json'
license=('MIT')
arch=('i686' 'x86_64')
makedepends=('cmake')
source=("https://github.com/nlohmann/json/archive/v${pkgver}.tar.gz")
sha256sums=('b20be343922f73504baf8a01543c2a42d16e01f18bb231b324a8959416ccf4f8')

build() {
  cd "json-${pkgver}"
  mkdir build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DJSON_BuildTests=OFF ..
  make
}

package() {
  cd "json-${pkgver}/build"
  make DESTDIR="${pkgdir}" install
  install -Dm644 ../LICENSE.MIT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.MIT"
}
