# Maintainer: Adrian Perez <aperez@igalia.com>

pkgname=beancount-hg
pkgver=r4675.h5c27e34a9ecf
pkgrel=1
pkgdesc='Double-Entry Accounting from Text Input'
arch=('i686' 'x86_64')
url='http://furius.ca/beancount'
license=('GPL')
depends=('python>=3.5' 'mpdecimal' 'python-ply' 'python-dateutil'
         'python-bottle' 'python-lxml')
makedepends=('mercurial')
optdepends=('python-magic-ahupp: importer cache (beancount.utils.file_type)'
            'python-chardet: importer cache (beancount.utils.file_type)'
            'python-beautifulsoup4: OFX importer (beancount.ingest.importers.ofx)'
            'python-google-api-python-client: uploading to Google Sheets (beancount.tools.sheets_upload), uploading/downloading Google Docs documentation')
provides=('beancount')
conflicts=('beancount')
source=("${pkgname}::hg+https://bitbucket.org/blais/beancount#branch=default")
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  echo r$(hg identify -nr.).h$(hg identify -ir.)
}

package() {
  cd "${pkgname}"
  python setup.py install --prefix=/usr --root="${pkgdir}"
}
