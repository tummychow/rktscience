# Maintainer: Jason Ryan <jasonwryan@gmail.com>
# Contributor: Steven Allen <steven@stebalien.com>
# Contributor: Army
# Contributor: Bastien Dejean <baskerville@lavabit.com>

pkgname=nsxiv-git
pkgver=28.r12.ge839638
pkgrel=1
pkgdesc='Neo Simple X image viewer'
arch=('i686' 'x86_64')
url='https://github.com/nsxiv/nsxiv'
license=('GPL2')
depends=('imlib2' 'giflib' 'libwebp' 'libexif' 'libxft' 'hicolor-icon-theme' 'xdg-utils' 'desktop-file-utils')
makedepends=('git')
provides=('sxiv' 'nsxiv')
conflicts=('sxiv' 'nsxiv')
source=("${pkgname}::git+https://github.com/nsxiv/nsxiv#branch=master"
        'config.patch')
sha256sums=('SKIP'
            '9c6d367c239679903168486fea2dd6dd2d75ba8d80634174ccfce47d1bcc5e64')

pkgver() {
  cd "${pkgname}"
  git describe --long --tags | sed 's/^v//g;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${pkgname}"
  for PATCH in "${source[@]}"; do
    if [[ "${PATCH}" == *.diff || "${PATCH}" == *.patch ]]; then
      patch -Np1 -i "${srcdir}/${PATCH}"
    fi
  done
}

build() {
  cd "${pkgname}"
  make PREFIX="/usr"
}

package() {
  cd "${pkgname}"
  make PREFIX=/usr DESTDIR="${pkgdir}" install-all
  ln -s /usr/bin/nsxiv "${pkgdir}/usr/bin/sxiv"
}
