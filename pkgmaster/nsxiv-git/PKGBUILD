# Maintainer: Jason Ryan <jasonwryan@gmail.com>
# Contributor: Steven Allen <steven@stebalien.com>
# Contributor: Army
# Contributor: Bastien Dejean <baskerville@lavabit.com>

pkgname=nsxiv-git
pkgver=28.r9.g8c54655
pkgrel=2
pkgdesc='Neo Simple X image viewer'
arch=('i686' 'x86_64')
url='https://github.com/nsxiv/nsxiv'
license=('GPL2')
depends=('imlib2' 'giflib' 'libwebp' 'libexif' 'libxft' 'hicolor-icon-theme' 'xdg-utils' 'desktop-file-utils')
makedepends=('git')
provides=('sxiv' 'nsxiv')
conflicts=('sxiv' 'nsxiv')
source=("${pkgname}::git+https://github.com/nsxiv/nsxiv#commit=8c5465514deab6c7f290ea89bbc1a8ac1da1245c"
        'config.patch')
sha256sums=('SKIP'
            '305cf456d2c9593bcb9061c76efcdc2cdbb0bda9e17d5a5f3365c1abeaab46fe')

pkgver() {
  cd "${pkgname}"
  git describe --long --tags | sed 's/^v//g;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${pkgname}"
  for PATCH in "${source[@]}"; do
    if [[ "${PATCH}" == *.diff || "${PATCH}" == *.patch ]]; then
      patch -Np1 -i "${srcdir}/${PATCH}"
    fi
  done
  ln -s config.def.h config.h
}

build() {
  cd "${pkgname}"
  make PREFIX="/usr"
}

package() {
  cd "${pkgname}"
  make PREFIX=/usr DESTDIR="${pkgdir}" install-all
  ln -s /usr/bin/nsxiv "${pkgdir}/usr/bin/sxiv"
}
