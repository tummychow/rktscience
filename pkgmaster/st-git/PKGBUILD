# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Scytrin dai Kinthra <scytrin@gmail.com>

pkgname=st-git
pkgver=r1042.g29f341d
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X'
arch=('i686' 'x86_64' 'armv7h')
url='https://st.suckless.org'
license=('MIT')
depends=('libxft')
makedepends=('ncurses' 'libxext' 'git')
provides=('st')
conflicts=('st')
source=("${pkgname}::git+https://git.suckless.org/st#branch=master"
        'st-scrollback-0.8.diff'
        'st-scrollback-mouse-0.8.diff'
        'st-scrollback-mouse-altscreen-0.8.diff'
        'config.patch')
sha256sums=('SKIP'
            '8279d347c70bc9b36f450ba15e1fd9ff62eedf49ce9258c35d7f1cfe38cca226'
            '3fb38940cc3bad3f9cd1e2a0796ebd0e48950a07860ecf8523a5afd0cd1b5a44'
            'e9f73c4de379d54ae107d0f4dd7b068a6628326c1bd7e11bfcd33d9079290347'
            '110a2d1b8e834d6b6ea6850b19e2e1ebf798561abab0bcdd8100796c9dac6cae')

pkgver() {
  cd "${pkgname}"
  echo r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)
}

prepare() {
  cd "${pkgname}"
  sed -i 's/CPPFLAGS =/CPPFLAGS +=/g' config.mk
  for PATCH in "${source[@]}"; do
    if [[ "${PATCH}" == *.diff || "${PATCH}" == *.patch ]]; then
      patch -Np1 -i "${srcdir}/${PATCH}"
    fi
  done
}

build() {
  cd "${pkgname}"
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "${pkgname}"
  export TERMINFO="${pkgdir}/usr/share/terminfo"
  install -d "${TERMINFO}"
  make PREFIX=/usr DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  rm -rf "${pkgdir}/usr/share/terminfo" # ncurses 5.9.5+ provides these now
}
