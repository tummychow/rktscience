# Maintainer: Jason Ryan <jasonwryan@gmail.com>
# Contributor: Steven Allen <steven@stebalien.com>
# Contributor: Army
# Contributor: Bastien Dejean <baskerville@lavabit.com>

pkgname=sxiv-git
pkgver=26.r0.g1d28627
pkgrel=1
pkgdesc='Simple X image viewer'
arch=('i686' 'x86_64')
url='https://github.com/muennich/sxiv'
license=('GPL2')
depends=('imlib2' 'libexif' 'libxft' 'hicolor-icon-theme' 'xdg-utils' 'desktop-file-utils')
makedepends=('git')
provides=('sxiv')
conflicts=('sxiv')
source=("${pkgname}::git+https://github.com/muennich/sxiv#branch=master"
        'config.patch')
sha256sums=('SKIP'
            '5dc9b22c261fb333b8b6ca99d692c7582723bdd48bcd2d8d44fc3d93f5135e41')

pkgver() {
  cd "${pkgname}"
  git describe --long --tags | sed 's/^v//g;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${pkgname}"
  for PATCH in "${source[@]}"; do
    if [[ "${PATCH}" == *.diff || "${PATCH}" == *.patch ]]; then
      patch -Np1 -i "${srcdir}/${PATCH}"
    fi
  done
}

build() {
  cd "${pkgname}"
  make PREFIX="/usr"
}

package() {
  cd "${pkgname}"
  make PREFIX=/usr DESTDIR="${pkgdir}" install
  make -C icon PREFIX=/usr DESTDIR="${pkgdir}" install
  install -Dm644 sxiv.desktop "${pkgdir}/usr/share/applications/sxiv.desktop"
}
