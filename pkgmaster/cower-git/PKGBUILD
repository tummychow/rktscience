# Maintainer: Dave Reisner <d@falconindy.com>

pkgname=cower-git
pkgver=16.r11.g00d6f92
pkgrel=1
pkgdesc='A simple AUR agent with a pretentious name'
arch=('i686' 'x86_64')
url='http://github.com/falconindy/cower'
license=('MIT')
depends=('curl' 'openssl' 'pacman' 'yajl')
makedepends=('git' 'perl')
conflicts=('cower')
provides=('cower')
source=("${pkgname}::git+https://github.com/falconindy/cower#branch=master")
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "${pkgname}"
  make
  sed '/^$/q' src/cower.c > LICENSE
}

package() {
  cd "${pkgname}"
  make PREFIX=/usr DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
