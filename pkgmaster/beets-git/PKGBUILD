# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Adrian Sampson <adrian@radbox.org>

pkgname=beets-git
pkgver=1.4.7.r167.g18281145
pkgrel=1
pkgdesc='Flexible music library manager and tagger'
arch=('any')
url='http://beets.radbox.org/'
license=('MIT')
depends=('python-jellyfish' 'python-munkres' 'python-musicbrainzngs'
         'python-mutagen' 'python-setuptools' 'python-six' 'python-unidecode'
         'python-yaml')
makedepends=('git' 'python-sphinx')
optdepends=('bash-completion: Bash completion'
            'bs1770gain: ReplayGain plugin'
            'gst-plugins-bad: Chromaprint/Acoustid, BPD, ReplayGain plugins'
            'gst-plugins-good: Chromaprint/Acoustid, BPD, ReplayGain plugins'
            'gst-plugins-ugly: Chromaprint/Acoustid, BPD, ReplayGain plugins'
            'gst-libav: Chromaprint/Acoustid, BPD, ReplayGain plugins'
            'gst-python: Chromaprint/Acoustid, BPD, ReplayGain plugins'
            'imagemagick: Thumbnails plugin'
            'python-beautifulsoup4: Lyrics plugin'
            'python-flask: Web plugin'
            'python-gobject: ReplayGain plugin'
            'python-mpd2: MPDStats plugin'
            'python-pyacoustid: Chromaprint/Acoustid plugin'
            'python-pylast: LastGenre plugin'
            'python-requests: Chromaprint/Acoustid, BPD, FetchArt plugins'
            'python-xdg: Thumbnails plugin')
provides=('beets')
conflicts=('beets')
source=("${pkgname}::git+https://github.com/beetbox/beets#branch=master"
        'beet')
sha256sums=('SKIP'
            'b081285b202f82d09802ebca73d28bbf1902179ad3a820e4c0157f80bf959d89')

pkgver() {
  cd "${pkgname}"
  git describe --long --tags | sed 's/^v//g;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "${pkgname}"
  python setup.py build sdist
}

package() {
  cd "${pkgname}"
  python setup.py install --root="${pkgdir}" --optimize='1' --skip-build

  install -dm 755 "${pkgdir}/usr/share/bash-completion/completions/"
  install -m 644 "${srcdir}/beet" "${pkgdir}/usr/share/bash-completion/completions/"
  install -dm 755 "${pkgdir}/usr/share/zsh/site-functions"
  install -m 644 extra/_beet "${pkgdir}/usr/share/zsh/site-functions/"

  install -dm 755 "${pkgdir}"/usr/share/man/man{1,5}
  install -m 644 man/beet.1 "${pkgdir}/usr/share/man/man1/"
  install -m 644 man/beetsconfig.5 "${pkgdir}/usr/share/man/man5/"

  install -dm 755 "${pkgdir}/usr/share/licenses/beets"
  install -m 644 LICENSE "${pkgdir}/usr/share/licenses/beets/"
}
