# Maintainer: Adrian Perez <aperez@igalia.com>

pkgname=beancount-git
pkgver=2.3.4.r21.g0003608e
pkgrel=1
pkgdesc='Double-Entry Accounting from Text Input'
arch=('i686' 'x86_64')
url='http://furius.ca/beancount'
license=('GPL')
depends=('python>=3.3' 'mpdecimal' 'python-ply' 'python-dateutil'
         'python-bottle' 'python-lxml')
makedepends=('git' 'python-setuptools')
optdepends=('python-magic-ahupp: importer cache (beancount.utils.file_type)'
            'python-chardet: importer cache (beancount.utils.file_type)'
            'python-beautifulsoup4: OFX importer (beancount.ingest.importers.ofx)'
            'python-google-api-python-client: uploading to Google Sheets (beancount.tools.sheets_upload), uploading/downloading Google Docs documentation')
provides=('beancount')
conflicts=('beancount')
source=("${pkgname}::git+https://github.com/beancount/beancount#branch=v2")
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

package() {
  cd "${pkgname}"
  python setup.py install --prefix=/usr --root="${pkgdir}"
}
